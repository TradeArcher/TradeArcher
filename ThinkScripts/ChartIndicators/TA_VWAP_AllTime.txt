#VWAP All Time Anchor Chart Indicator
#Author: TradeArcher2020
#Version: 0.1
#Date Created: 10/04/2021

#This indicator will calculate the VWAP from the beginning of the chart however far back the chart goes.
#Hint: If you want the VWAP from the beginning of when a ticker started trading, then you need to show that far back on the chart.


input numDev1 = 1.0;
input numDev2 = 2.0;

def isPeriodRolled = compoundValue(1, no, yes);

def volumeSum;
def volumeVwapSum;
def volumeVwap2Sum;

if (isPeriodRolled) {
    volumeSum = volume;
    volumeVwapSum = volume * vwap;
    volumeVwap2Sum = volume * Sqr(vwap);
} else {
    volumeSum = compoundValue(1, volumeSum[1] + volume, volume);
    volumeVwapSum = compoundValue(1, volumeVwapSum[1] + volume * vwap, volume * vwap);
    volumeVwap2Sum = compoundValue(1, volumeVwap2Sum[1] + volume * Sqr(vwap), volume * Sqr(vwap));
}
def price = volumeVwapSum / volumeSum;
def deviation = Sqrt(Max(volumeVwap2Sum / volumeSum - Sqr(price), 0));

plot VWAP = price;
plot UpperBand = price + numDev1 * deviation;
plot LowerBand = price - numDev1 * deviation;

plot UpperBand2 = price + numDev2 * deviation;
plot LowerBand2 = price - numDev2 * deviation;

VWAP.setDefaultColor(Color.LIGHT_ORANGE);

UpperBand.setStyle(Curve.SHORT_DASH);
LowerBand.setStyle(Curve.SHORT_DASH);

UpperBand2.setStyle(Curve.LONG_DASH);
LowerBand2.setStyle(Curve.LONG_DASH);

UpperBand.setDefaultColor(Color.LIGHT_GREEN);
LowerBand.setDefaultColor(Color.LIGHT_GREEN);

UpperBand2.setDefaultColor(Color.LIGHT_GREEN);
LowerBand2.setDefaultColor(Color.LIGHT_GREEN);
