#Spikes and Deathcandles Plus Asshole Meter
#Author: TradeArcher2020
#Version: 0.1
#Date Created: 10/09/2021

#HINT: This indicator tries to identify spikes and deathcandles.  As an added bonus, it includes an Asshole Meter that measures the number of spikes and death candles within the specified lookback period and the choppier the ticker is, the more of an asshole it labels it.  

input atrLength = 60;
input atrFactor = 4;
input minATRGreater = 2;
input minATRLess = 2;
input lookback = 15;
input showBubbles = no;
input showAssholeMeter = yes;

def currentATR = ATR(atrLength)[1];

def tr = TrueRange(high, close, low);

def isBigCandle = tr / currentATR >= atrFactor;

def isRed = close < open;

def candleBodyTop = if(isRed, open, close);
def candleBodyBottom = if(isRed, close, open);

def isDeathCandle = isRed and isBigCandle and (close - low) / (high - low) <= 0.15 and candleBodyBottom + (currentATR * minATRLess) < candleBodyBottom[1];

def isSpikeCandle = !isRed and isBigCandle and (high - close) / (high - low) <= 0.15 and candleBodyTop - (currentATR * minATRGreater) > candleBodyTop[1];

def bn = BarNumber();

def isRollover = bn % lookback == 0 or GetDay() != GetDay()[1] or SecondsFromTime(0930) == 0 and SecondsTillTime(0930) == 0;

def dcCount = if(isRollover, if(isDeathCandle, 1, 0), if(isDeathCandle, dcCount[1] + 1, dcCount[1] + 0));
def scCount = if(isRollover, if(isSpikeCandle, 1, 0), if(isSpikeCandle, scCount[1] + 1, scCount[1] + 0));

def bcRatio = if(scCount > 0, AbsValue(dcCount / scCount), 0);

def assholeMeter = if(bcRatio > 0 and bcRatio < 2 and dcCount > 0 and scCount > 0, dcCount + scCount, 0);

AddChartBubble(showBubbles and assholeMeter > 1 and assholeMeter > assholeMeter[1], high, "Asshole Meter: " + if assholeMeter == 1 then "ANNOYING!" else if assholeMeter == 2 then "TOYING WITH YOU!" else if assholeMeter == 3 then "ASSHOLE!" else "TOTAL DICK!!!", if assholeMeter == 1 then Color.YELLOW else if assholeMeter == 2 then Color.ORANGE else if assholeMeter == 3 then Color.DARK_RED else Color.LIGHT_RED);

AddLabel(assholeMeter > 1, "Asshole Meter: " + if assholeMeter == 2 then "ANNOYING!" else if assholeMeter == 3 then "TOYING WITH YOU!" else if assholeMeter == 4 then "ASSHOLE!" else "TOTAL DICK!!!", if assholeMeter == 2 then Color.YELLOW else if assholeMeter == 3 then Color.ORANGE else if assholeMeter == 4 then Color.DARK_RED else Color.LIGHT_RED);

AddLabel(showAssholeMeter and assholeMeter <= 1, "Asshole Meter: Playing by the rules.", Color.WHITE);

DefineGlobalColor("DeathCandle", Color.MAGENTA);
DefineGlobalColor("BullishSpikeCandle", Color.CYAN);
AssignPriceColor(if isDeathCandle then GlobalColor("DeathCandle") else if isSpikeCandle then GlobalColor("BullishSpikeCandle") else Color.CURRENT);