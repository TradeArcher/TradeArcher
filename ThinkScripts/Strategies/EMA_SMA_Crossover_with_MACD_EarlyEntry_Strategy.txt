#EMA SMA Crossover with MACD Strategy Early Entry
#Author: TradeArcher2020
#Version: 0.2
#Date Created: 10/14/2020

input shortEMALength = 8;
input midMALength = 12;
input smaLength = 26;
input ensureUptrend = no;
input earlyEntry = yes;
input conservativeSell = no;

input uptrendPeriod = 5;
input includePreMarket = no;
input candleAggregationPeriod = AggregationPeriod.MIN;

input MACDMinValue = 0.0;
input MACDEnsureMAsBelowZero = no;

input avgVolPeriod = 5;

input percentAcceptableLoss = 0.5;

input accountSize = 40000;
input accountPercent = 10;
input maxShares = 1000;
input minAverageVolumeForFullSize = 75000;

def buyOption = open(priceType = PriceType.BID);
def sellPrice = open(priceType = PriceType.ASK);

#**************************************************
script EMA_SMA_Crossover_with_MACD_EarlyEntry_Indicator {

#EMA SMA Crossover with MACD Strategy Early Entry Indicator
#Author: TradeArcher2020
#Version: 0.2
#Date Created: 11/11/2020

input shortEMALength = 8;
input midMALength = 12;
input smaLength = 26;
input ensureUptrend = no;
input earlyEntry = yes;
input conservativeSell = no;

input avgVolPeriod = 5;
input uptrendPeriod = 5;
input includePreMarket = no;
input candleAggregationPeriod = AggregationPeriod.MIN;

input MACDMinValue = 0.0;
input MACDEnsureMAsBelowZero = no;

def buyOption = open(priceType = PriceType.BID);
def sellPrice = open(priceType = PriceType.ASK);
input minAverageVolumeForFullSize = 75000;

def shortMA = MovAvgExponential( length = shortEMALength );
def midMA = SimpleMovingAvg( length = midMALength );
def longMA = SimpleMovingAvg( length = smaLength  );
def macdShortMA = MACD();
def macdLongMA = MACD().Avg;
def macdDiff = MACD().Diff;

def hasMinVolume = VolumeAvg(length = avgVolPeriod) >= minAverageVolumeForFullSize;

def isWithinTradingHours = If (includePreMarket, yes, RegularTradingStart(GetYYYYMMDD()) < GetTime() and  RegularTradingEnd(GetYYYYMMDD()) > GetTime());

def isTrendingUp = If(!ensureUptrend, yes, shortMA > shortMA[uptrendPeriod] and longMA > longMA[uptrendPeriod - 1]);

def areMACDMAsBelowZero = if(MACDEnsureMAsBelowZero, (macdLongMA <= 0.0 and macdShortMA <= 0.0), yes);

def avgPrcRange = (if(open[1] > close[1], open[1] - close[1], close[1] - open[1]) + if(open[2] > close[2], open[2] - close[2], close[2] - open[2]) + if(open[3] > close[3], open[3] - close[3], close[3] - open[3]) + if(open[4] > close[4], open[4] - close[4], close[4] - open[4]) + if(open[5] > close[5], open[5] - close[5], close[5] - open[5])) / 5;

def crossoverSignal = shortMA[1] < longMA[1] and shortMA[0] >= longMA[0];

def earlyEntrySignal = (shortMA < longMA and (longMA - shortMA) <= (avgPrcRange / 2)) or crossoverSignal;

def buy = if(earlyEntry, earlyEntrySignal, crossoverSignal) and isWithinTradingHours and isTrendingUp and macdDiff >= MACDMinValue and areMACDMAsBelowZero;

def currentAggPeriod = GetAggregationPeriod();
def aggPeriodType = 
if currentAggPeriod >= AggregationPeriod.YEAR 
then AggregationPeriod.YEAR
else 
    if currentAggPeriod >= AggregationPeriod.DAY  
    then AggregationPeriod.DAY
    else 
        if currentAggPeriod >= AggregationPeriod.HOUR 
        then AggregationPeriod.HOUR
        else AggregationPeriod.MIN;

plot buySignal = if(buy and !(buy[1] or buy[2] or buy[3] or buy[4] or buy[5]), yes, no);
buySignal.SetPaintingStrategy(paintingStrategy = PaintingStrategy.BOOLEAN_ARROW_UP);
buySignal.SetDefaultColor(color = Color.LIME);
buySignal.SetLineWeight(lineWeight = 3);

def askPrice = open(priceType = PriceType.ASK);

plot cutLossLine = longMA -(longMA - (longMA - avgPrcRange));
cutLossLine.SetPaintingStrategy(paintingStrategy = PaintingStrategy.HORIZONTAL);
cutLossLine.SetDefaultColor(color = Color.PINK);
cutLossLine.SetLineWeight(lineWeight = 3);
cutLossLine.SetHiding(yes);

def sell = 
if earlyEntry then
    (buy or buy[1] or buy[2] or buy[3] or buy[4]) == no
    and
    if(conservativeSell, (shortMA <= midMA and shortMA[1] > midMA[1]), (shortMA <= longMA and shortMA[1] > longMA[1]))
else
    if(conservativeSell, (shortMA <= midMA and shortMA[1] > midMA[1]), (shortMA <= longMA and shortMA[1] > longMA[1]));

def failed = (shortMA < longMA and (buySignal[1] or buySignal[2]) and askPrice < cutLossLine);

plot sellSignal = (sell or failed) and isWithinTradingHours;
sellSignal.SetPaintingStrategy(paintingStrategy = PaintingStrategy.BOOLEAN_ARROW_DOWN);

def buyPrice = if(buySignal, buyOption, buyPrice[1]);

def isLoss = buyPrice[1] > sellPrice;

#AddChartBubble(sellSignal, color = Color.WHITE, text = buyPrice + "\n" + sellPrice, "price location" = HIGH + 0.15);

sellSignal.AssignValueColor(color = if (failed) then Color.RED else (if (isLoss) then Color.RED else Color.LIME) );
sellSignal.SetLineWeight(lineWeight = 3);

}
#**************************************************

def hasMinVolume = VolumeAvg(length = avgVolPeriod) >= minAverageVolumeForFullSize;

def isWithinTradingHours = If (includePreMarket, yes, RegularTradingStart(GetYYYYMMDD()) < GetTime() and  RegularTradingEnd(GetYYYYMMDD()) > GetTime());

def baseNumberOfShares = If (hasMinVolume, Min(Round( (accountSize * accountPercent / 100) / buyOption[-1], numberOfDigits = 0 ), maxShares), Min(Round( (accountSize * (accountPercent / 4) / 100) / buyOption[-1], numberOfDigits = 0 ), maxShares / 2));

def numberOfShares = If(!isWithinTradingHours, AbsValue(baseNumberOfShares / 4), baseNumberOfShares);

def currentAggPeriod = GetAggregationPeriod();
def aggPeriodType = 
if currentAggPeriod >= AggregationPeriod.YEAR 
then AggregationPeriod.YEAR
else 
    if currentAggPeriod >= AggregationPeriod.DAY  
    then AggregationPeriod.DAY
    else 
        if currentAggPeriod >= AggregationPeriod.HOUR 
        then AggregationPeriod.HOUR
        else AggregationPeriod.MIN;

def buy = EMA_SMA_Crossover_with_MACD_EarlyEntry_Indicator(shortEMALength, midMALength, smaLength, ensureUptrend, earlyEntry, conservativeSell, avgVolPeriod, uptrendPeriod, includePreMarket, candleAggregationPeriod, macdMinValue, macdEnsureMAsBelowZero, minAverageVolumeForFullSize).buySignal;
def sell = EMA_SMA_Crossover_with_MACD_EarlyEntry_Indicator(shortEMALength, midMALength, smaLength, ensureUptrend, earlyEntry, conservativeSell, avgVolPeriod, uptrendPeriod, includePreMarket, candleAggregationPeriod, macdMinValue, macdEnsureMAsBelowZero, minAverageVolumeForFullSize).sellSignal;

addOrder( OrderType.BUY_TO_OPEN, buy, tradeSize = numberOfShares, price = buyOption[0], tickColor = Color.LIME, arrowColor = Color.LIME, name = "EMA_SMA_Crossover_with_MACD_EarlyEntry BUY[" + (currentAggPeriod / aggPeriodType) + (if aggPeriodType == AggregationPeriod.YEAR then "Y" else if aggPeriodType == AggregationPeriod.DAY then "D" else if aggPeriodType == AggregationPeriod.HOUR then "H" else "M") + ", " + shortEMALength + "," + midMALength + "," + smaLength + "," + ensureUptrend + "," + earlyEntry + "," + conservativeSell + "," + uptrendPeriod + "," + includePreMarket + "," + candleAggregationPeriod + "," + MACDMinValue + "," + MACDEnsureMAsBelowZero + "," + avgVolPeriod + "," + percentAcceptableLoss + "] BUY @" + buyOption[0] );

def buyPrice = EntryPrice();

def isGain = sellPrice > buyPrice;

def percentGainLoss = ((sellPrice - buyPrice) / sellPrice) * 100;

def cutLoss =  (buyPrice > close and percentGainLoss < (PercentAcceptableLoss * -1));

#AddChartBubble("time condition" = yes, "price location" = HIGH, text = "1:" + (buyPrice > close) + "\n2:" + percentGainLoss + "\n3:" + (PercentAcceptableLoss * -1));

AddOrder( OrderType.SELL_TO_CLOSE, sell and isGain, tradeSize = numberOfShares, price = sellPrice, tickColor = Color.UPTICK, arrowColor = Color.UPTICK, name = "EMA_SMA_Crossover_with_MACD_EarlyEntry BUY[" + (currentAggPeriod / aggPeriodType) + (if aggPeriodType == AggregationPeriod.YEAR then "Y" else if aggPeriodType == AggregationPeriod.DAY then "D" else if aggPeriodType == AggregationPeriod.HOUR then "H" else "M") + ", " + shortEMALength + "," + midMALength + "," + smaLength + "," + ensureUptrend + "," + earlyEntry + "," + conservativeSell + "," + uptrendPeriod + "," + includePreMarket + "," + candleAggregationPeriod + "," + MACDMinValue + "," + MACDEnsureMAsBelowZero + "," + avgVolPeriod + "," + percentAcceptableLoss + "] SELL @" + sellPrice + " P/L%: " + percentGainLoss );

AddOrder( OrderType.SELL_TO_CLOSE, sell and !isGain, tradeSize = numberOfShares, price = sellPrice, tickColor = Color.DOWNTICK, arrowColor = Color.DOWNTICK, name = "EMA_SMA_Crossover_with_MACD_EarlyEntry BUY[" + (currentAggPeriod / aggPeriodType) + (if aggPeriodType == AggregationPeriod.YEAR then "Y" else if aggPeriodType == AggregationPeriod.DAY then "D" else if aggPeriodType == AggregationPeriod.HOUR then "H" else "M") + ", " + shortEMALength + "," + midMALength + "," + smaLength + "," + ensureUptrend + "," + earlyEntry + "," + conservativeSell + "," + uptrendPeriod + "," + includePreMarket + "," + candleAggregationPeriod + "," + MACDMinValue + "," + MACDEnsureMAsBelowZero + "," + avgVolPeriod + "," + percentAcceptableLoss + "] SELL @" + sellPrice + " P/L%: " + percentGainLoss );